plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    defaultConfig {
        applicationId "com.thirdwavelist.coficiando"

        versionCode 5
        versionName "1.1.0"

        proguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("src/main/release.keystore")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }

        debug {
            storeFile file("src/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            events "started", "skipped", "passed", "failed"
            showStandardStreams true
        }
    }

    lintOptions {
        checkReleaseBuilds true
        quiet false
        abortOnError true
        ignoreWarnings false
        xmlReport true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/**'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.composeVersion.get()
    }
}

hilt {
    enableAggregatingTask = true
}

dependencies {
    implementation libs.kotlin.stdlib

    implementation project(":libraries:core-utils")
    implementation project(":libraries:core")
    implementation project(":libraries:navigation")
    implementation project(":libraries:design")
    implementation project(":features:home")
    implementation project(":features:details")
    implementation project(":features:settings")

    implementation platform(libs.firebase.bom)
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-config-ktx"

    implementation libs.bundles.compose
    implementation libs.bundles.compose.ui
    implementation libs.compose.ui.material
    implementation libs.compose.ui.insets
    implementation libs.compose.ui.activity

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat

    implementation libs.hilt.android
    kapt libs.hilt.compiler

    debugImplementation libs.leakcanary
}

apply plugin: 'com.google.gms.google-services'