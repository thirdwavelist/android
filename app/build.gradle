plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.firebase.crashlytics'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.appdistribution'
    alias(libs.plugins.tripletPlay.gradlePlugin)
    alias(libs.plugins.googleServices.gradlePlugin)
}

android {
    defaultConfig {
        applicationId "com.thirdwavelist.coficiando"

        versionCode 6
        versionName "1.1.0"

        proguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            storePassword System.getenv("ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }

        debug {
            storeFile file("src/debug/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat "full"
            events "started", "skipped", "passed", "failed"
            showStandardStreams true
        }
    }

    lintOptions {
        checkReleaseBuilds true
        quiet false
        abortOnError true
        ignoreWarnings false
        xmlReport true
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            firebaseAppDistribution {
                artifactType="AAB"
            }

        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude 'META-INF/**'
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeVersion.get()
    }
}

hilt {
    enableAggregatingTask = true
}

play {
    track.set("alpha")
}

dependencies {
    implementation projects.libraries.coreUtils
    implementation projects.libraries.core
    implementation projects.libraries.navigation
    implementation projects.libraries.design
    implementation projects.features.home
    implementation projects.features.details
    implementation projects.features.settings

    implementation platform(libs.firebase.bom)
    implementation libs.firebase.crashlytics
    implementation libs.firebase.analytics
    implementation libs.firebase.config

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material.design
    implementation libs.androidx.constraintlayout
    implementation libs.bundles.androidx.navigation.component

    implementation libs.hilt.android
    kapt libs.hilt.compiler

    debugImplementation libs.leakcanary
}

apply from: 'https://raw.githubusercontent.com/JakeWharton/SdkSearch/master/gradle/projectDependencyGraph.gradle'